#+ summary: Count genomic features in a genome graph.
#+ tags:
#+  - Count genomic features
#+ enumerate:
#+  - graph:
#+    - http://solgenomics.net/genome/Solanum_lycopersicum
#+    - http://solgenomics.net/genome/Solanum_pennellii
#+    - http://solgenomics.net/genome/Solanum_tuberosum
#+    - http://plants.ensembl.org/Solanum_lycopersicum
#+    - http://plants.ensembl.org/Solanum_tuberosum

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://www.geneontology.org/formats/oboInOwl#>

SELECT
   ?feature_id
   ?feature_name
   (COUNT(*) AS ?n)
WHERE {
   GRAPH ?_graph_iri {
      ?feature a ?feature_uri .
   }
   GRAPH <http://purl.obolibrary.org/obo/so.owl> {
      ?feature_uri a owl:Class ;
      rdfs:label ?feature_name ;
      obo:id ?feature_id .
   }
}
GROUP BY ?feature_id ?feature_name
ORDER BY DESC(?n)