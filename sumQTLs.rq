#+ summary: Summarize QTLs extracted from articles.
#+ tags:
#+  - Summarize QTLs

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dcmitype: <http://purl.org/dc/dcmitype/>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>
PREFIX so: <http://purl.obolibrary.org/obo/so#>
PREFIX QTL: <http://purl.obolibrary.org/obo/SO_0000771>

SELECT
  (CONCAT('NCBITaxon:', REPLACE(STR(?taxon_uri), '.+_', ''))  AS ?taxon_id)
  (COUNT(DISTINCT ?pmcid) AS ?n_articles)
  (COUNT(DISTINCT ?qtl) AS ?n_qtls)
  (COUNT(DISTINCT ?loc) AS ?n_qtls_with_loc)
FROM <http://europepmc.org>
WHERE {
  ?dts a dcmitype:Dataset ;
    so:genome_of ?taxon_uri ;
    dcterms:hasPart ?qtl .
  ?qtl a QTL: ;
    dcterms:isReferencedBy ?pmcid .
  OPTIONAL {
    ?qtl faldo:location ?loc
  }
}
GROUP BY ?taxon_uri
