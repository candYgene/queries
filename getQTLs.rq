#+ summary: Get QTLs associated with a trait identified by ID.
#+ tags:
#+  - Get QTLs associated with a trait
#+ defaults:
#+  - traitid: "SP:0000201"

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX QTL: <http://purl.obolibrary.org/obo/SO_0000771>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX so: <http://purl.obolibrary.org/obo/so#>

SELECT
  ?qtl_id
  (CONCAT('NCBITaxon:', REPLACE(STR(?taxon_uri), '.+_', ''))  AS ?taxon_id)
WHERE {
  GRAPH <http://europepmc.org> {
    SELECT
      *
    WHERE {
      ?qtl a QTL: ;
        dcterms:identifier ?qtl_id ;
        obo:RO_0003308 ?trait_id ;
        ^dcterms:hasPart/so:genome_of ?taxon_uri .
      FILTER(?trait_id = URI(CONCAT(obo:, REPLACE(?_traitid, ':', '_'))))
    }
  }
}
